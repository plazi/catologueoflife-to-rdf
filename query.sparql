PREFIX dwc: <http://rs.tdwg.org/dwc/terms/>
PREFIX col: <https://terms.catalogueoflife.org/>
PREFIX dcterms: <http://purl.org/dc/terms/>

CONSTRUCT {
  ?taxon 
      # dwc:taxonName ?dwc_scientificName;
      dwc:parent ?parent;                                           # Used by Synospecies
      dwc:acceptedName ?acceptedName;                               # Used by Synospecies
      dwc:originalName ?originalName;
      # dwc:rank ?dwc_taxonRank;
      # dwc:genus ?gbif_genericName;
      # dwc:species ?dwc_specificEpithet;

      ## All tsv columns in order:
      # dwc:taxonID ?dwc_taxonID ;
      # dwc:parentNameUsageID ?dwc_parentNameUsageID ;
      # dwc:acceptedNameUsageID ?dwc_acceptedNameUsageID ;
      # dwc:originalNameUsageID ?dwc_originalNameUsageID ;
      # dwc:scientificNameID ?dwc_scientificNameID ;
      dwc:datasetID ?dwc_datasetID ;
      dwc:taxonomicStatus ?dwc_taxonomicStatus ;                    # Used by Synospecies
      dwc:taxonRank ?dwc_taxonRank ;                                # Used by Synospecies
      dwc:scientificName ?dwc_scientificName ;                      # Used by Synospecies
      dwc:scientificNameAuthorship ?dwc_scientificNameAuthorship ;  # Used by Synospecies
      col:notho ?col_notho ;
      dwc:genericName ?dwc_genericName ;                            # Used by Synospecies
      dwc:infragenericEpithet ?dwc_infragenericEpithet ;            # Used by Synospecies
      dwc:specificEpithet ?dwc_specificEpithet ;                    # Used by Synospecies
      dwc:infraspecificEpithet ?dwc_infraspecificEpithet ;          # Used by Synospecies
      dwc:cultivarEpithet ?dwc_cultivarEpithet ;
      dwc:nameAccordingTo ?dwc_nameAccordingTo ;
      dwc:namePublishedIn ?dwc_namePublishedIn ;
      dwc:nomenclaturalCode ?dwc_nomenclaturalCode ;
      dwc:nomenclaturalStatus ?dwc_nomenclaturalStatus ;
      dwc:taxonRemarks ?dwc_taxonRemarks ;
      dcterms:references ?dcterms_references ;
} WHERE {
  bind(iri(concat("https://www.catalogueoflife.org/data/taxon/",?dwc_taxonID)) as ?taxon)
  bind(iri(concat("https://www.catalogueoflife.org/data/taxon/",?dwc_parentNameUsageID)) as ?parent)
  bind(iri(concat("https://www.catalogueoflife.org/data/taxon/",?dwc_acceptedNameUsageID)) as ?acceptedName)
  bind(iri(concat("https://www.catalogueoflife.org/data/taxon/",?dwc_originalNameUsageID)) as ?originalName)
}

# tarql allows multiple CONSTRUCT statements, and allows querying previously constructed triples in them
CONSTRUCT { ?taxon dwc:kingdom ?kingdom . }
WHERE {
  ?taxon dwc:taxonRank "kingdom" ;
         dwc:scientificName ?full_name .
  OPTIONAL { ?taxon dwc:scientificNameAuthorship ?authority . }
  BIND(REPLACE(?full_name, CONCAT(" ", COALESCE(?authority, ""), "$"), "") AS ?kingdom)
  # In some cases (8 of 23), the kingdom has an authority. the above should trim it.
}
CONSTRUCT { ?taxon dwc:kingdom ?kingdom . }
WHERE { ?taxon dwc:acceptedName?/dwc:parent*/dwc:kingdom ?kingdom . }
